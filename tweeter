#!/usr/bin/perl

use strict;
use warnings;

use FindBin '$Bin';
use lib "$Bin/lib";

use DBI;
use Apollo::Twitter;

my $t = Apollo::Twitter->new(
    traits          => [ 'API::RESTv1_1', 'OAuth' ],
    ssl             => 1,
    consumer_key    => $ENV{APOLLO_KEY},
    consumer_secret => $ENV{APOLLO_SEC},
  );

$t->authorise;

my $sel_sql = 'select rowid, message, orig_time
               from   status
	       where  new_time <= strftime("%Y-%m-%dT%H:%M:%S",
	                                   DATETIME("now"))
               and    done = 0
               order by new_time asc';

my $upd_sql = 'update status
               set    done = 1
	       where  rowid = ?';

my $dbh = DBI->connect('dbi:SQLite:dbname=apollo.db')
  or die DBI->errstr;

my $sel_sth = $dbh->prepare($sel_sql);
$sel_sth->execute;

my $upd_sth = $dbh->prepare($upd_sql);

while (my $status = $sel_sth->fetch) {
  my $msg = "$status->[1] ($status->[2] UTC) #apollo11 #apollo50";
  warn localtime . " Tweeting: $msg\n";
  $t->update($msg);
  $upd_sth->execute($status->[0]);
  sleep 1;
}

